apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'

android {
  signingConfigs {
    releaseconfig {}
    debugconfig {}
  }

  compileSdkVersion 25
  buildToolsVersion "25.0.2"

  defaultConfig {
    applicationId "com.denysnovoa.nzbmanager"
    minSdkVersion 17
    targetSdkVersion 25
    versionCode 1
    versionName "1.0"
    testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
  }

  productFlavors {
    dev {
      minSdkVersion 21
      signingConfig signingConfigs.releaseconfig
    }
    pro {
      minSdkVersion 17
      signingConfig signingConfigs.releaseconfig
    }
  }

  variantFilter { variant ->
    def names = variant.flavors*.name

    if ((names.contains("dev") && variant.buildType.name == "release") || (names.contains(
        "pro") && variant.buildType.name == "debug")) {
      variant.ignore = true
    }
  }


  buildTypes {
    debug {
      debuggable true
      applicationIdSuffix(".develop")
    }

    release {
      minifyEnabled false
      proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
    }
  }

  packagingOptions {
    exclude 'META-INF/DEPENDENCIES'
    exclude 'META-INF/NOTICE'
    exclude 'META-INF/LICENSE'
    exclude 'META-INF/LICENSE.txt'
    exclude 'META-INF/NOTICE.txt'
    exclude 'META-INF/rxjava.properties'
  }
}

dependencies {
  def retrofit2 = '2.2.0'

  compile fileTree(dir: 'libs', include: ['*.jar'])
  compile 'com.android.support:appcompat-v7:25.3.1'
  compile 'com.android.support:recyclerview-v7:25.3.1'

  compile 'com.android.support.constraint:constraint-layout:1.0.2'
  compile "org.jetbrains.kotlin:kotlin-stdlib-jre7:$kotlin_version"

  compile("com.squareup.retrofit2:retrofit:$retrofit2") {
    exclude module: 'okhttp'
  }
  compile "com.squareup.retrofit2:adapter-rxjava:$retrofit2"
  compile "com.squareup.retrofit2:converter-gson:$retrofit2"

  compile 'com.squareup.okhttp3:okhttp:3.6.0'
  compile 'com.squareup.okhttp3:logging-interceptor:3.6.0'

  compile 'com.squareup.picasso:picasso:2.5.2'

  compile 'nl.littlerobots.rxlint:rxlint:1.2'
  compile 'io.reactivex.rxjava2:rxandroid:2.0.1'
  compile "io.reactivex.rxjava2:rxjava:2.0.9"

  androidTestCompile('com.android.support.test.espresso:espresso-core:2.2.2', {
    exclude group: 'com.android.support', module: 'support-annotations'
  })
  testCompile 'junit:junit:4.12'
}
repositories {
  mavenCentral()
}
